apply plugin: 'com.android.application'

import java.util.Properties
import java.io.FileInputStream

def envFile = rootProject.file(".env")
def googleClientId = ""

if (envFile.exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(envFile))
    googleClientId = properties.getProperty("VITE_GOOGLE_CLIENT_ID")
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "br.unicamp.ic.mih"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "br.unicamp.ic.mih"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "1.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
        resValue "string", "server_client_id", googleClientId
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties.getProperty("storeFile") ?: "mih-release-key.jks")
            storePassword keystoreProperties.getProperty("storePassword")
            keyAlias keystoreProperties.getProperty("keyAlias")
            keyPassword keystoreProperties.getProperty("keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
    implementation "com.google.android.gms:play-services-auth:20.2.0"
}

apply from: 'capacitor.build.gradle'